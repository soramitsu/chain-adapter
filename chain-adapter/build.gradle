/*
 * Copyright D3 Ledger, Inc. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.hazelcast_version=4.1
    repositories {
        mavenCentral()
        jcenter()
        // gradle plugins repository
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
    }
}
plugins {
    id 'jp.co.soramitsu.sora-plugin' version '0.1.2'
}

apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'

mainClassName = 'com.d3.chainadapter.ChainAdapterMain'
group 'com.d3.chainadapter'

shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'
// name of jar file that the shadowJar plugin generates
shadowJar.archiveName = 'app.jar'

// sora-plugin configs
soramitsu {
    projectGroup = 'soramitsu'
    docker {
        // docker tag
        tag = System.getenv("DOCKER_TAG")
        // jar file that is used in the generated Dockerfile
        jar = new File("build/libs/app.jar")
        // the rest in configured using env variables
    }
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs = ["src/integration-test/resources"]
        }
    }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs chain-adapter integration tests'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    // Enable JUnit5 tests
    useJUnitPlatform {
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task runIrohaChainAdapter(type: JavaExec) {
    main = 'com.d3.chainadapter.ChainAdapterMain'
    classpath = sourceSets.main.runtimeClasspath
}

dependencies {
    implementation project(":chain-adapter-client")
    implementation("com.github.d3ledger.notary:notary-commons:$notary_version")
    implementation("com.hazelcast:hazelcast:$hazelcast_version")
    implementation("org.springframework:spring-context:5.1.4.RELEASE")

    testImplementation("org.springframework.boot:spring-boot-starter-test:2.4.0") {
        exclude module: "logback-classic"
    }
    testImplementation("org.junit.vintage:junit-vintage-engine:5.2.0")
    testImplementation("com.github.d3ledger.notary:notary-iroha-integration-test:$notary_version")
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.2.0')
    testImplementation("org.mockito:mockito-all:2.0.2-beta")
    testImplementation("io.mockk:mockk:1.10.2")
}

project.afterEvaluate {
    dockerfileCreate.dependsOn(shadowJar)
    integrationTest.dependsOn(dockerfileCreate)
    test.finalizedBy(integrationTest)
}
